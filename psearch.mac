/* psearch.mac

検索結果をポップアップ表示する秀丸マクロ。
（xyzzyのpsearchに相当するマクロです。）


【動作】
カーソル位置にある単語を検索し、検索結果をメニューで一覧表示します。
更に、メニューから単語のある行へジャンプすることができます。

選択の「あり・なし」で検索方法が変わります。
    選択無し：カーソル位置の単語を検索。
    選択あり：選択された文字を検索。（単語では検索しない）


【動作を確認した環境】
・秀丸エディタ 8.40 b40
・田楽DLL
  http://www.ceres.dti.ne.jp/~sugiura/
  田楽DLL Ver.3.20で動作することを確認しています、別途インストールしてください。


【オリジナルの作者】
秀丸エディタスレ Part30
178 ：名無しさん＠お腹いっぱい。 ：2010/10/14(木) 02:08:50 ID:7v+i/hHB0 (2 回発言)


【連絡先】
ohtorii
http://d.hatena.ne.jp/ohtorii/
https://github.com/ohtorii
*/


////////////////////////////////////////////////////////////////////////////
//	カスタマイズ
////////////////////////////////////////////////////////////////////////////
//メニューに検索結果を幾つ表示するか。
#g_max_num = 100;




////////////////////////////////////////////////////////////////////////////
//	メイン処理
////////////////////////////////////////////////////////////////////////////
$g_tabspace="";
$g_searchbuffer = searchbuffer;
#g_searchoption = searchoption;
call Main;
setsearch $g_searchbuffer, #g_searchoption;
endmacro;



Main:
	call LoadDengaku;
	if(! ##return){
		return false;
	}
	call SetupTabSpace;
	
	#old_lineno = lineno;
	#lineno[-1] = lineno;
	#column[-1] = column;
	#search_word = false;
	if(! selecting){
		selectword;
		#search_word=true;
	}

	$word = gettext(seltopx, seltopy, selendx, selendy);
	if(0==strlen($word)){
		return ;
	}

	disabledraw;
	gofiletop;
	if(#search_word){
		searchdown2 $word, word,nocasesense,noregular,nohilight;
	}else{
		searchdown2 $word,nocasesense,noregular,nohilight;
	}
	hilightfound 1;

	#cur_pos_index = -1;
	#prev_line_no = -1;
	while (result && (#i < #g_max_num)) {
		if(lineno != #prev_line_no){
			if(lineno == #old_lineno){
				#cur_pos_index = #i;
				$mark = "→";
			}else{
				$mark = "  ";
			}

			call GetLineStr;
			$line_str = $$return;
			$m[#i] = $mark + str(lineno) + ": " + $line_str;
			#lineno[#i] = lineno;
			#column[#i] = column;
		}
		#i = #i + 1;
		#prev_line_no = lineno;
		finddown;
	}
	movetolineno #column[-1] + 1, #lineno[-1];
	enabledraw;
	menuarray $m, #i;

	if (result){
		if(#cur_pos_index != (result-1)){
			#r = result-1;
			disabledraw;
			movetolineno #column[#r] + 1, #lineno[#r];

			//カーソルを画面中心にする
			//enabledraw y - (windowheight/2);
		}
	}
	return ;

GetLineStr:
	escape;
	golinetop2;
	beginsel;
	golineend2;
	endsel;
	$$text	= gettext2(seltopcolumn, seltoplineno, selendcolumn, selendlineno);
	call TrimString $$text;
	$$text = $$return;
	
	//& -> &&へ変換する。この処理がないとメニューで文字の下にアンダーバーが付く。
	$$text=dllfuncstr("GSUB",$$text,"&","&&",-1);
	
	
	//タブ文字を空白文字へ変換する。
	$$text = dllfuncstr("GSUB", $$text, "\t", "    ", -1);
	return $$text;
	

LoadDengaku:
	//田楽ＤＬＬをロード
	loaddll ( hidemarudir + "\\DengakuDLL.dll" );
	if ( !result ){
		message "田楽ＤＬＬをロードできませんでした。";
		return false;
	}
	//
	//田楽ＤＬＬのバージョンチェック
	$dllver = leftstr ( dllfuncstr("GETVERSION") ,1 ) + rightstr ( dllfuncstr("GETVERSION") ,2 );
	if ( val ( $dllver ) <= 204 ) {
		message "このマクロの実行には Ver2.04 以降の田楽ＤＬＬが必要です。";
		freedll;
		return false;
	}
	return true;

SetupTabSpace:
	$g_tabspace="";
	##i=0;
	while(##i<tabcount){
		$g_tabspace=$g_tabspace+" ";
		##i=##i+1;
	}
	return;
	
TrimString:
	$$space="\r\n\t 　";//空白文字を定義
	while(1){
	  ##word=strlen($$1);
	  //文字列の先頭に空白文字がある場合、空白文字を削除
	  if(strstr($$space,leftstr($$1,2))!=-1)$$1=rightstr($$1,##word-2);
	  else if(strstr($$space,rightstr($$1,2))!=-1)$$1=leftstr($$1,##word-2);
	  //ここまで全角文字の対処
	  else if(strstr($$space,leftstr($$1,1))!=-1)$$1=rightstr($$1,##word-1);
	  else if(strstr($$space,rightstr($$1,1))!=-1)$$1=leftstr($$1,##word-1);
	  else break;
	}
	return $$1;